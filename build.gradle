plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.8'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'com.diffplug.spotless' version '6.25.0'
}

group = 'org.todaybook'
version = '0.0.1-SNAPSHOT'
description = 'A worker service that generates book embeddings with VertexAI and OpenSearch'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
    maven {
        name = "GitHubPublicPackages"
        url = uri("https://maven.pkg.github.com/today-book/common")
        credentials {
            username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
            password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
        }
    }
}

ext {
    springAiVersion = '1.1.0'
    commonVersion = '1.3.3'
    resilience4jVersion = '2.2.0'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.ai:spring-ai-bom:${springAiVersion}"
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation "org.springframework.boot:spring-boot-starter-aop"
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation "io.github.resilience4j:resilience4j-spring-boot3:${resilience4jVersion}"

    implementation "org.todaybook:common-core:${commonVersion}"

    // database
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    runtimeOnly 'org.postgresql:postgresql'

    testImplementation 'org.springframework.boot:spring-boot-testcontainers'
    testImplementation "org.testcontainers:testcontainers-postgresql:2.0.2"
    testImplementation "org.testcontainers:testcontainers-junit-jupiter:2.0.2"
    testImplementation "org.opensearch:opensearch-testcontainers:2.0.2"

    // spring ai - embedding
    implementation 'org.springframework.ai:spring-ai-vertex-ai-embedding'
    implementation 'org.springframework.ai:spring-ai-starter-vector-store-opensearch'

    // spring batch
    implementation 'org.springframework.boot:spring-boot-starter-batch'
    testImplementation 'org.springframework.batch:spring-batch-test'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // monitoring
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    runtimeOnly 'io.micrometer:micrometer-registry-prometheus'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

spotless {
    java {
        importOrder(
                'java|javax|jakarta',
                'org.springframework',
                'lombok',
                '',
                'org.junit|org.mockito',
                '\\#',
                '\\#org.junit'
        )
        googleJavaFormat()
        formatAnnotations()
        removeUnusedImports()
        trimTrailingWhitespace()
        endWithNewline()

        target 'src/**/*.java'
    }
}

tasks.named('test') {
    dependsOn(tasks.named("spotlessApply"))
    useJUnitPlatform()
}
